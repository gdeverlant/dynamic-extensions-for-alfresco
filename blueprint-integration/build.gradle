subprojects {
    apply plugin: "osgi"
    apply plugin: "kotlin"

    dependencies {
        compileOnly "${project.ext.alfrescoBom}"

        compile project(":annotations-runtime:annotations-runtime-${project.ext.simpleAlfrescoVersion}")
        compile project(":alfresco-integration:alfresco-integration-${project.ext.simpleAlfrescoVersion}")

        compileOnly("org.alfresco.surf:spring-webscripts")
        compileOnly("org.alfresco:alfresco-repository")

        compileOnly "org.osgi:org.osgi.core:${project.ext.osgiVersion}"

        compileOnly "javax.servlet:servlet-api:${project.ext.servletVersion}"
        compileOnly "com.springsource.util:com.springsource.util.osgi:${project.ext.springOsgiUtilVersion}"
        compile project(":event-bus:event-bus-${project.ext.simpleAlfrescoVersion}")
    }

    if(new File("${project.projectDir}/dependencies.gradle").exists()) {
        // Don't use default dependencies but the ones that work with that Alfresco version
        apply from: "${project.projectDir}/dependencies.gradle"
    } else {
        apply from: "${project.parent.projectDir}/default-dependencies.gradle"
    }

    jar {
        manifest {
            instruction 'Fragment-Host', 'org.eclipse.gemini.blueprint.extender'
            instruction 'DynamicImport-Package', '*'
            instruction 'Bundle-DocURL', 'https://github.com/xenit-eu/dynamic-extensions-for-alfresco'
        }
    }
}