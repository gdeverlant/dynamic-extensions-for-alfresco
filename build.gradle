import eu.xenit.dynamicextensionsalfresco.Versions

buildscript {
    ext.kotlin_version = '1.0.3'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.8.0"
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    version = '4.10'
}

apply plugin: 'io.codearte.nexus-staging'

nexusStaging {
    username = project.hasProperty('de_publish_username') ? project.de_publish_username : ''
    password = project.hasProperty('de_publish_password') ? project.de_publish_password : ''
    packageGroup = "eu.xenit"
}

allprojects {
    group = 'eu.xenit.de'
    version = '2.0.0-SNAPSHOT'
    
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    repositories {
        maven {
            url "${rootProject.projectDir}/alfresco-bom-repo"
        }
        mavenCentral()
        maven {
            url "https://artifacts.alfresco.com/nexus/content/groups/public/"
        }
        jcenter()
        maven {
            url "http://repository.springsource.com/maven/bundles/release"
        }
        maven {
            url "http://repository.springsource.com/maven/bundles/external"
        }
    }
}

// Configuration that should be applied to all subprojects. (If a sub project also has sub projects for different 
// Alfresco versions, it should not build any artifacts.)
def allSubProjectsConfig = {
    buildscript {
        repositories {
            mavenCentral()
        }
        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        }
    }

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven'
    apply plugin: 'signing'

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    artifacts {
        archives sourceJar, javadocJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                signing {
                    required { false }
                    sign configurations.archives
                }
                beforeDeployment {
                    MavenDeployment deployment ->

                        signing.signPom(deployment)

                }

                repository(url: project.hasProperty('de_publish_url') ? project.de_publish_url : "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: project.hasProperty('de_publish_username') ? project.de_publish_username : '', password: project.hasProperty('de_publish_password') ? project.de_publish_password : '')
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: project.hasProperty('de_publish_username') ? project.de_publish_username : '', password: project.hasProperty('de_publish_password') ? project.de_publish_password : '')
                }

                pom.project {
                    url 'https://github.com/xenit-eu/dynamic-extensions-for-alfresco'
                    name "dynamic-extensions-for-alfresco-" + project.name
                    description "Adds an OSGi container to alfresco repository supporting dynamic code reloading, " +
                            "classpath isolation and a bunch of other useful features"

                    scm {
                        connection 'scm:git:git@github.com:xenit-eu/dynamic-extensions-for-alfresco.git'
                        developerConnection 'scm:git:git@github.com:xenit-eu/dynamic-extensions-for-alfresco.git'
                        url 'https://github.com/xenit-eu/dynamic-extensions-for-alfresco.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id "laurentvdl"
                            name "Laurent Van der Linden"
                            roles {
                                role "Founder"
                                role "Architect"
                            }
                        }
                        developer {
                            id 'stanmine'
                            name 'Stan Mine'
                            email 'stan.mine@xenit.eu'
                            roles {
                                role "Architect"
                                role "Developer"
                            }
                        }
                        developer {
                            id 'yregaieg'
                            name 'Younes Regaieg'
                            email 'younes.regaieg@xenit.eu'
                            roles {
                                role "Contributor"
                            }
                        }
                    }
                }
            }
        }
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    compileJava {
        sourceCompatibility = 1.7
        targetCompatibility = 1.7
        options.compilerArgs << '-Xlint:unchecked'
    }

    ext {
        simpleAlfrescoVersion = Versions.getSimpleAlfrescoVersionFromProjectName(project.name)
        alfrescoVersion = Versions.alfrescoVersions.get(simpleAlfrescoVersion)
        springVersion = Versions.springVersions.get(simpleAlfrescoVersion)

        alfrescoBom = "eu.xenit.alfresco:alfresco-${Versions.alfrescoEdition}-bom:${alfrescoVersion}"

        
        osgiVersion = '4.3.1'
        geminiVersion = '1.0.2.RELEASE'
        felixVersion = '4.2.1'

        slf4jVersion = '1.7.7'
        junitVersion = '4.12'
        transactionApiVersion = '1.1'

        springOsgiUtilVersion = '2.0.5.RELEASE'
        mockitoVersion = '1.10.19'
        jibxVersion = '1.2.3'
        hibernateVersion = '3.2.6.ga'
        servletVersion = '2.5'
        bndVersion = '2.4.1'
    }

    configurations.all {
        exclude group: 'jaxen'
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'org.springframework') {
                    details.useVersion project.ext.springVersion
                }

                if (details.requested.group == 'org.alfresco.surf') {
                    if (project.alfrescoVersion.startsWith('5.0')) {
                        details.useTarget group: 'org.springframework.extensions.surf',
                                name: details.requested.name,
                                version: project.alfrescoVersion
                        details.because "'org.springframework.extensions.surf' was moved to 'org.alfresco.surf'"
                    }
                }
            }
        }
    }

    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(uploadArchives)) {
            if (!project.hasProperty('keyId') || !project.hasProperty('secretKeyRingFile') || !project.hasProperty('password')) {
                throw new GradleException('You need to provide signing params in order to sign artifacts')
            }

            def id = project.hasProperty('keyId') ? project.keyId : ''
            def file = project.hasProperty('secretKeyRingFile') ? project.secretKeyRingFile : ''
            def password = project.hasProperty('password') ? project.password : ''

            ext."signing.keyId" = id
            ext."signing.secretKeyRingFile" = file
            ext."signing.password" = password
        }
    }
}

// Configuration specific for projects that are split up into Alfresco-version subprojects
def subSubProjectsConfig = {
    afterEvaluate {
        sourceSets {
            if (project.plugins.hasPlugin("kotlin")) {
                main.kotlin.srcDirs += "${project.parent.projectDir}/shared/src/main/kotlin"
                test.kotlin.srcDirs += "${project.parent.projectDir}/shared/src/test/kotlin"
            }
            if (project.plugins.hasPlugin("groovy")) {
                main.groovy.srcDirs += "${project.parent.projectDir}/shared/src/main/groovy"
                test.groovy.srcDirs += "${project.parent.projectDir}/shared/src/test/groovy"
            }
            main.java.srcDirs += "${project.parent.projectDir}/shared/src/main/java"
            test.java.srcDirs += "${project.parent.projectDir}/shared/src/test/java"
            main.resources.srcDirs += "${project.parent.projectDir}/shared/src/main/resources"
            test.resources.srcDirs += "${project.parent.projectDir}/shared/src/test/resources"

            if (project.hasProperty("excludedSharedFiles")) {
                def excludeFilter = {
                    file ->
                        if (file.file.path.contains("shared") && project.ext.excludedSharedFiles.contains(file.name)) {
                            return true
                        }
                }
                // We should use a different source file for this Alfresco version, the shared source will be excluded
                main.java.exclude excludeFilter
                javadoc.exclude excludeFilter
            }
        }
    }
}

configure((subprojects - project(':gradle-plugin') - project(':annotations')), {
    subprojects allSubProjectsConfig << subSubProjectsConfig
})
configure([project(':gradle-plugin'), project(':annotations')], allSubProjectsConfig)

defaultTasks 'build'
